apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId 'io.github.allefsousa'
                artifactId 'android-totp'
                version '0.0.1'

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    artifact("$buildDir/libs/${project.getName()}-${version}.jar")
                }

                artifact androidSourcesJar

                // Mostly self-explanatory metadata
                pom {
                    name = 'android-totp'
                    description = 'Android totp library'
                    url = 'https://github.com/allefsousa/android-totp'
                    licenses {
                        license {
                            name = 'Apache License'
                            url = 'https://github.com/elye/loaderviewlibrary/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'allefsousa'
                            name = 'Allef sousa'
                            email = 'allefsousa1@gmail.com'
                        }
                    }

                    // Version control info - if you're using GitHub, follow the
                    // format as seen here
                    scm {
                        connection = 'scm:git:github.com/allefsousa/android-totp.git'
                        developerConnection = 'scm:git:ssh://github.com/allefsousa/android-totp.git'
                        url = 'https://github.com/allefsousa/android-totp/tree/main'
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}
